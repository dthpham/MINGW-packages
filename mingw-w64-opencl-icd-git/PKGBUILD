# Maintainer: Duong Pham <dthpham@gmail.com>

_realname=opencl-icd
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_commit=cb4acb99df3441e147bd859c20d9c3533ec95ce8
pkgver=r5.cb4acb9
pkgrel=1
pkgdesc="OpenCL Installable Client Driver (ICD) Loader (mingw-w64)"
arch=('any')
url="https://www.khronos.org/registry/OpenCL/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-opencl-headers"
             "git")
options=('strip')
source=("${_realname}"::"git+https://github.com/KhronosGroup/OpenCL-ICD-Loader.git#commit=""${_commit}"
        0001-use-hkey-current-user-hive.patch)
sha256sums=('SKIP'
            '37dd6193b9dca2c4a7c68ce8d9d2f6206d3c2c94f64d374fa16ddaa676b65dbc')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -Np1 -i ${srcdir}/0001-use-hkey-current-user-hive.patch
  cd ..
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    ../${_realname}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  install -Dm755 bin/libOpenCL.dll "${pkgdir}${MINGW_PREFIX}"/bin/libOpenCL.dll
  install -Dm644 libOpenCL.dll.a "${pkgdir}${MINGW_PREFIX}"/lib/libOpenCL.dll.a
  install -Dm644 "../${_realname}"/LICENSE.txt "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
